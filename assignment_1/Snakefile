SUBJECTS = [f'subj{i}' for i in range(1, 7)]

rule all:
    input:
        'plots/average.png',
        expand('plots/{subj}.png', subj=SUBJECTS)

rule download:
    output: directory('data/{subj}')
    shell: 'wget http://data.pymvpa.org/datasets/haxby2001/{wildcards.subj}-2010.01.14.tar.gz -O - | tar -xzf - -C data/'

rule searchlight_analysis:
    input: directory('data/{subj}')
    output: 'processed_data/{subj}.npz'
    shell: 'python src/searchlight_analysis.py {input} {output}'

rule plot:
    input: 'processed_data/{file}.npz'
    output: 'plots/{file}.png'
    shell: 'python src/plot.py {input} {output}'

rule calculate_average:
    input: expand('processed_data/{subj}.npz', subj=SUBJECTS)
    output: 'processed_data/average.npz'
    shell: 'python src/average.py {input} {output}'
